buildscript {
	repositories {
		mavenCentral()
		google()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:8.5.2'
		classpath 'com.google.gms:google-services:4.3.14'
		classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
		classpath 'com.android.tools.build:gradle:8.5.2'
	}
}

repositories {
	mavenCentral()
	google()
}

apply plugin: 'com.android.application'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

dependencies {
	def lifecycle_version = '2.8.4'
	implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

	implementation 'androidx.annotation:annotation:1.8.2'
	implementation 'androidx.appcompat:appcompat:1.7.0'
	implementation 'androidx.recyclerview:recyclerview:1.3.2'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
	implementation 'androidx.preference:preference:1.2.1'
	implementation 'com.google.android.material:material:1.12.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation 'androidx.fragment:fragment:1.8.2'

	implementation 'com.fasterxml.jackson.core:jackson-core:2.17.2'
	implementation 'org.apache.commons:commons-lang3:3.16.0'
	implementation 'org.apache.commons:commons-text:1.12.0'
	implementation 'joda-time:joda-time:2.12.7'
	implementation 'com.squareup.okhttp3:okhttp:4.12.0'
	implementation 'info.guardianproject.netcipher:netcipher:2.1.0'
	implementation 'com.google.android.exoplayer:exoplayer-core:2.19.1'
	implementation 'com.google.android.exoplayer:exoplayer-ui:2.19.1'
	implementation 'com.github.luben:zstd-jni:1.5.6-4@aar'

	implementation 'androidx.wear:wear:1.3.0'
	implementation 'com.google.android.support:wearable:2.9.0'
	compileOnly 'com.google.android.wearable:wearable:2.9.0'

	testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.6.1'

	implementation platform('com.google.firebase:firebase-bom:33.1.2')
	implementation 'com.google.firebase:firebase-analytics'
	implementation 'com.google.firebase:firebase-crashlytics'
}

android {

	compileSdk 34

	defaultConfig {
		applicationId "dev.steenbakker.wearreader"
		minSdkVersion 23
		targetSdkVersion 34
		versionCode 11
		versionName "1.0.1"

		vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
		testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
	}
	// Flag to tell aapt to keep the attribute ids around
	aaptOptions {
		additionalParameters "--no-version-vectors"
	}

	buildTypes.all {
		minifyEnabled true
		shrinkResources false

		proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
	}

	compileOptions {
		encoding = "UTF-8"
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	lintOptions {
		checkReleaseBuilds false
		abortOnError true
		warningsAsErrors true

		error 'DefaultLocale'

		// Translations are crowd-sourced
		disable 'MissingTranslation'
		disable 'MissingQuantity'

		// Temporarily disabled
		disable 'ImpliedQuantity'
		disable 'InflateParams'
		disable 'ObsoleteSdkInt'
		disable 'OldTargetApi'
		disable 'AndroidGradlePluginVersion'

		// Too many false positives
		disable 'UnusedResources'

		// Not an issue
		disable 'ViewConstructor'
		disable 'GradleDependency'

		// Caused by 3rd party dependencies/jetification
		disable 'ObsoleteLintCustomCheck'
	}

	packagingOptions {
		exclude 'META-INF/*'
	}

	sourceSets {
		main {
			assets.srcDirs = ['assets']
		}
	}

	testOptions {
		animationsDisabled = true
	}
	namespace 'org.quantumbadger.redreader'
}

pmd {
	toolVersion = '6.36.0'
	consoleOutput = true
}

task pmd(type: Pmd) {
	dependsOn 'assembleDebug'
	ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml")
	ruleSets = [] // otherwise defaults clash with the list in rules.xml
	source 'src/main/java/org/quantumbadger'
	include '**/*.java'
}

checkstyle {
	configFile rootProject.file("${project.rootDir}/config/checkstyle/checkstyle.xml")
	ignoreFailures false
	showViolations true
	maxWarnings 0
	toolVersion = "8.35"
}

task Checkstyle(type: Checkstyle) {
	source 'src/main/java/org/quantumbadger'
	ignoreFailures false
	showViolations true
	include '**/*.java'
	classpath = files()
}
